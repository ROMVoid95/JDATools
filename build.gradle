buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.21.0'
    }
}

plugins {
    id 'eclipse'
    id 'java-library'
    id "com.github.ben-manes.versions" version "0.42.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'signing'
}

apply plugin: "com.vanniktech.maven.publish"

version = "3.0.2-SNAPSHOT"
group = 'com.readonlydev'
archivesBaseName = 'jdatools'

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def javaVersion = JavaVersion.VERSION_17

configurations {
    resolveableImpl {
        transitive = true
        canBeResolved = true
    }
    api.extendsFrom resolveableImpl
}

sourceSets {
    test {
        java.srcDirs += 'src/test/java'
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

apply from: 'dependencies.gradle'

repositories {
    mavenCentral()
}

dependencies {
    api jda()
    api guava()
    api lang3()
    api json()
    api jacksonCore()
    api jacksonDatabind()
    api jacksonAnnotations()

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

def projectVersion = version.toString()
if(projectVersion.endsWith("SNAPSHOT")) {
    projectVersion = projectVersion.replace("-", ".")
}
def splitVerison = projectVersion.tokenize(".")

task sourcesForRelease(type: Copy) {
    from('src/main/java') {
        filter(ReplaceTokens, tokens: [
            MAJOR:	splitVerison.get(0),
            MINOR:    	splitVerison.get(1),
            PATCH:      splitVerison.get(2)
        ])
    }
    into 'build/filteredSrc'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    source = sourcesForRelease.destinationDir
    classpath += sourceSets.main.compileClasspath

    dependsOn sourcesForRelease
}

def setMainManifestProps = { Manifest manifest->
    manifest.mainAttributes ([
        'Implementation-Title':     archivesBaseName,
        'Implementation-Version':   version,
        'Implementation-Vendor':    'ReadOnly Development',
        'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}

task fullBuild {
    group 'jdatools'
    dependsOn 'clean'
    dependsOn 'build'
    tasks.findByName('build').mustRunAfter 'clean'
}

def majorJavaVersion = javaVersion.getMajorVersion()

javadoc {
    options.addBooleanOption('html5', true)
    options.addStringOption('Xdoclint:none', '-quiet')
    options.setWindowTitle(archivesBaseName + ' | ' + projectVersion)
    options.setDocTitle(archivesBaseName + ' | ' + projectVersion)
    options.setEncoding('UTF-8')
    options.tags 'apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:', 'implNote:a:Implementation Note:'
    options.links = [
        "https://docs.oracle.com/en/java/javase/${majorJavaVersion}/docs/api".toString(),
        "https://javadoc.io/doc/net.dv8tion/JDA/${jdaVersion}".toString()
    ]

    destinationDir = file("$buildDir/docs/")
}

jar {
    manifest(setMainManifestProps)
    classifier = 'lite'
}

shadowJar {
    classifier = ''
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

build {
    dependsOn { clean }
    dependsOn { javadocJar }
    dependsOn { shadowJar }
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

mavenPublishing {
    signAllPublications()
    pomFromGradleProperties()
}
//
//publishing {
//    publications {
//        JDATools(MavenPublication) {
//            artifact jar
//            artifact shadowJar
//            artifact sourcesJar
//            artifact javadocJar
//        }
//    }
//    repositories {
//        maven {
//            url "file://${project.projectDir}/repo"
//        }
//    }
//}

task localMavenPublish {
    group 'jdatools'
    dependsOn 'clean'
    dependsOn 'publishToMavenLocal'
    tasks.findByName('publishToMavenLocal').mustRunAfter 'clean'
}
